*** Settings ***
Library    Selenium2Library
Resource    ../variables.txt

**keywords**
Login Page::Login As
    [Arguments]    ${user}    ${rememberMe}=${False}
    Input Text    //form//*[@id='username']    ${user}[username]
    Input Text    //form//*[@id='password']    ${user}[password]
    Run Keyword If    '${rememberMe}'=='${True}'    Click Element    //form//*[@id='remember_me']
    Click Element    //form//*[@id='submit']

Login Page::Wait Until Login Form Is Visible
    ${usernameField} =    Set Variable    //form//*[@id='username']
    ${passwordField} =    Set Variable    //form//*[@id='password']
    ${rememberMeCheckBox} =    Set Variable    //form//*[@id='remember_me']
    ${loginButton} =    Set Variable    //form//*[@id='submit']
    Wait Until Element Is Visible    ${usernameField}    ${shortPeriodOfTime}    'Username Field Should Be Visible'
    Wait Until Element Is Visible    ${passwordField}    ${shortPeriodOfTime}    'Password Field Should Be Visible'
    Wait Until Element Is Visible    ${rememberMeCheckBox}    ${shortPeriodOfTime}    'Remember Me Check Box Should Be Visible'
    Wait Until Element Is Visible    ${loginButton}    ${shortPeriodOfTime}    'Login Button Should Be Visible'
    Do Not Have An Account Button Should Be Visible
    Forgot Password Button Should Be Visible

Forgot Password Page::Wait Until Forgot Password Form Is Visible
    ${emailField} =    Set Variable    //form//*[@id='email']
    ${registerButton} =    Set Variable    //form//*[@id='submit']
    Wait Until Element Is Visible    ${emailField}    ${shortPeriodOfTime}    'Email Field Should Be Visible'
    Wait Until Element Is Visible    ${registerButton}    ${shortPeriodOfTime}    'Register Button Should Be Visible'

Login Page::Click Do Not Have An Account Button
    ${doNotHaveAnAccountButton} =    Do Not Have An Account Button Should Be Visible
    Click Element    ${doNotHaveAnAccountButton}

Login Page::Click Forgot Password Button
    ${forgotPasswordButton} =    Forgot Password Button Should Be Visible
    Click Element    ${forgotPasswordButton}

Do Not Have An Account Button Should Be Visible
    ${doNotHaveAnAccountButton} =    Set Variable    //*[contains(@class, 'card-footer')]//*[normalize-space()='Do not have an account?']
    Wait Until Element Is Visible    ${doNotHaveAnAccountButton}    ${shortPeriodOfTime}    'Do Not Have An Account Button Should Be Visible'
    [Return]    ${doNotHaveAnAccountButton}

Forgot Password Button Should Be Visible
    ${forgotPasswordButton} =    Set Variable    //*[contains(@class, 'card-footer')]//*[normalize-space()='Forgot Your Password?']
    Wait Until Element Is Visible    ${forgotPasswordButton}    ${shortPeriodOfTime}    'Forgot Password Button Should Be Visible'
    [Return]    ${forgotPasswordButton}

Verify Login As Customer
    Login Page::Wait Until Login Form Is Visible
    Login Page::Login As    ${customer}
    Flash::Wait Until Flash Message With Category Is Visible    Login successful!    success
    Common::Wait Until Navbar Contains    Home    Products    Shopping Cart    ${customer}[username]    Logout
    [Teardown]    Run Keywords    Logout
    ...                    AND    Go to Login Page

Verify Login As Staff
    Login Page::Wait Until Login Form Is Visible
    Login Page::Login As    ${staff}
    Flash::Wait Until Flash Message With Category Is Visible    Login successful!    success
    Common::Wait Until Navbar Contains    Home    Products    Orders    Promotions    ${staff}[username]    Logout
    [Teardown]    Run Keywords    Logout
    ...                    AND    Go to Login Page

Verify Login As Admin
    Login Page::Wait Until Login Form Is Visible
    Login Page::Login As    ${admin}
    Flash::Wait Until Flash Message With Category Is Visible    Login successful!    success
    Common::Wait Until Navbar Contains    Home    Products    Manage Users    ${admin}[username]    Logout
    [Teardown]    Run Keywords    Logout
    ...                    AND    Go to Login Page

Verify Do not Have An Account Button
    Login Page::Wait Until Login Form Is Visible
    Login Page::Click Do Not Have An Account Button
    Register Page::Wait Until Register Form Is Visible
    [Teardown]    Go to Login Page

Verify Forgot Password Button
    Login Page::Wait Until Login Form Is Visible
    Login Page::Click Forgot Password Button
    Forgot Password Page::Wait Until Forgot Password Form Is Visible
    [Teardown]    Go to Login Page
