*** Settings ***
Library    Selenium2Library
Resource    ../allVariables.txt

*** keywords ***
Common::Wait Until Navbar Button Is Visible
    [Arguments]    ${buttonID}
    ${button} =    Set Variable    //*[contains(@class, 'navbar')]//*[contains(@id, '${buttonID}')]
    Wait Until Element Is Visible    ${button}    ${shortPeriodOfTime}    '${buttonID} Button Should Be Visible'
    [return]    ${button}

Common::Wait Until Navbar Contains
    [Arguments]    @{buttonNames}
    FOR    ${buttonName}    IN    @{buttonNames}
        Common::Wait Until Navbar Button Is Visible    ${buttonName}
    END

Common::Click Navbar Button
    [arguments]    ${buttonID}
    ${button} =    Common::Wait Until Navbar Button Is Visible    ${buttonID}
    Click Element    ${button}

Flash::Wait Until Flash Message With Category Is Visible
    [Arguments]    ${message}    ${category}
    ${flaskBox} =    Set Variable    //*[contains(@class, 'alert-${category}')]//*[contains(@class, 'flash')]
    ${flaskMessage} =    Set Variable    //*[contains(@class, 'alert')]//*[contains(@class, 'flash') and normalize-space()='${message}']
    ${flaskCloseButton} =    Set Variable    //*[contains(@class, 'alert')]//*[contains(@class, 'flash')]//button[contains(@class, 'btn-close')]
    Wait Until Element Is Visible    ${flaskBox}    ${shortPeriodOfTime}    'Flash Box Should Be Visible'
    Wait Until Element Is Visible    ${flaskMessage}    ${shortPeriodOfTime}    'Flash Message Should Be Visible'
    Wait Until Element Is Visible    ${flaskCloseButton}    ${shortPeriodOfTime}    'Flash Close Button Should Be Visible'

Go To Home Page
    Common::Click Navbar Button    Home
    Common::Wait Until Navbar Contains    Home

Go to Login Page
    Common::Click Navbar Button    Login
    Common::Wait Until Navbar Contains    Home    Login    SignUp

Go To Register Page
    Common::Click Navbar Button    SignUp
    Common::Wait Until Navbar Contains    Home    Login    SignUp

Go To Manage Products Page
    Common::Click Navbar Button    ManageProduct
    Common::Wait Until Navbar Contains    Home    ManageProduct    ManagePromotions    ManageOrders    UserProfile   Logout

Go To ShoppingCart Page
    Common::Click Navbar Button    ShoppingCart
    Common::Wait Until Navbar Contains    Home    ShoppingCart    UserProfile    Logout

Go To manage Order Page
    Common::Click Navbar Button    ManageOrders
    Common::Wait Until Navbar Contains    Home    ManageProduct    ManagePromotions    ManageOrders    UserProfile   Logout

Go To Manage Discount Page
    Common::Click Navbar Button    ManagePromotions
    Common::Wait Until Navbar Contains    Home    ManageProduct    ManagePromotions    ManageOrders    UserProfile   Logout

Go To Manage User Page
    Common::Click Navbar Button    ManageUser
    Common::Wait Until Navbar Contains    Home    ManageUser    UserProfile    Logout

Logout
    Common::Click Navbar Button    Logout
    Flash::Wait Until Flash Message With Category Is Visible    Logout successful!    success
    Common::Wait Until Navbar Contains    Home    Login    SignUp
